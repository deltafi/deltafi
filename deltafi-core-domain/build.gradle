plugins {
	id "com.github.hierynomus.license" version "${hierynomusLicenseVersion}"
	id 'com.netflix.dgs.codegen' version "${dgsCodegenVersion}"
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'org.springframework.boot' version "${springBootVersion}"
	id "com.palantir.docker" version "${palantirDockerVersion}"
}

description = 'Core domain for DeltaFi'

license {
	mapping("graphql", "SCRIPT_STYLE")
	header(rootProject.file('HEADER'))
	excludes(excludeFromLicense)
}

bootJar {
	archiveFileName = "${project.name}.jar"
}

jar {
	archiveClassifier = '' // boot plugin sets this to plain, clear it
	include('org/deltafi/core/domain/api/**')
	include('org/deltafi/core/domain/generated/client/**')
	include('org/deltafi/core/domain/generated/types/**')
}

generateJava {
    packageName = 'org.deltafi.core.domain.generated'
    generateClient = true
    typeMapping = [
	  "JSON": "java.util.Map<String, Object>",
	  "Long": "java.lang.Long",
	  "Action": "org.deltafi.common.types.Action",
	  "ActionDescriptor": "org.deltafi.common.types.ActionDescriptor",
	  "ActionDescriptorInput": "org.deltafi.common.types.ActionDescriptor",
	  "ActionEventInput": "org.deltafi.common.types.ActionEventInput",
	  "ActionEventType": "org.deltafi.common.types.ActionEventType",
	  "ActionRegistrationInput": "org.deltafi.common.types.ActionRegistrationInput",
	  "ActionSchema": "org.deltafi.core.domain.types.ActionSchema",
	  "ActionState": "org.deltafi.common.types.ActionState",
	  "Content": "org.deltafi.common.types.Content",
	  "ContentInput": "org.deltafi.common.types.Content",
	  "ContentReference": "org.deltafi.common.content.ContentReference",
	  "ContentReferenceInput": "org.deltafi.common.content.ContentReference",
	  "DataType": "org.deltafi.common.types.VariableDataType",
	  "DeletePolicy": "org.deltafi.core.domain.types.DeletePolicy",
	  "DeltaFile": "org.deltafi.common.types.DeltaFile",
	  "DeltaFileStage": "org.deltafi.common.types.DeltaFileStage",
	  "DeltaFiles": "org.deltafi.core.domain.types.DeltaFiles",
	  "Domain": "org.deltafi.common.types.Domain",
	  "DomainInput": "org.deltafi.common.types.Domain",
	  "EgressActionSchemaInput": "org.deltafi.common.types.EgressActionSchemaInput",
	  "EgressFlow": "org.deltafi.core.domain.types.EgressFlow",
	  "EgressFlowPlan": "org.deltafi.core.domain.types.EgressFlowPlan",
	  "EnrichActionSchemaInput": "org.deltafi.common.types.EnrichActionSchemaInput",
	  "EnrichFlow": "org.deltafi.core.domain.types.EnrichFlow",
	  "EnrichFlowPlan": "org.deltafi.core.domain.types.EnrichFlowPlan",
	  "EnrichInput": "org.deltafi.common.types.EnrichInput",
	  "Enrichment": "org.deltafi.common.types.Enrichment",
	  "EnrichmentInput": "org.deltafi.common.types.Enrichment",
	  "ErrorDomain": "org.deltafi.common.types.ErrorDomain",
	  "ErrorInput": "org.deltafi.common.types.ErrorInput",
	  "FilterInput": "org.deltafi.common.types.FilterInput",
	  "FormatActionSchemaInput": "org.deltafi.common.types.FormatActionSchemaInput",
	  "FormatInput": "org.deltafi.common.types.FormatInput",
	  "FormattedData": "org.deltafi.common.types.FormattedData",
	  "FormattedDataInput": "org.deltafi.common.types.FormattedData",
	  "IngressFlow": "org.deltafi.core.domain.types.IngressFlow",
	  "IngressFlowPlan": "org.deltafi.core.domain.types.IngressFlowPlan",
	  "IngressInput": "org.deltafi.common.types.IngressInput",
	  "KeyValue": "org.deltafi.common.types.KeyValue",
	  "KeyValueInput": "org.deltafi.common.types.KeyValue",
	  "LoadActionSchemaInput": "org.deltafi.common.types.LoadActionSchemaInput",
	  "LoadInput": "org.deltafi.common.types.LoadInput",
	  "Plugin": "org.deltafi.common.types.Plugin",
	  "Metadata": "java.util.Map<String, String>",
	  "PluginCoordinates": "org.deltafi.common.types.PluginCoordinates",
	  "PluginCoordinatesInput": "org.deltafi.common.types.PluginCoordinates",
	  "Property": "org.deltafi.common.types.Property",
	  "PropertyId": "org.deltafi.core.domain.types.PropertyId",
	  "PropertyInput": "org.deltafi.core.domain.types.Property",
	  "PropertySet": "org.deltafi.common.types.PropertySet",
	  "PropertySetInput": "org.deltafi.common.types.PropertySet",
	  "PropertySource": "org.deltafi.core.domain.types.PropertySource",
	  "PropertyUpdate": "org.deltafi.core.domain.types.PropertyUpdate",
	  "ProtocolLayer": "org.deltafi.common.types.ProtocolLayer",
	  "ProtocolLayerInput": "org.deltafi.common.types.ProtocolLayer",
	  "SourceInfo": "org.deltafi.common.types.SourceInfo",
	  "SourceInfoInput": "org.deltafi.common.types.SourceInfo",
	  "SplitInput": "org.deltafi.common.types.SplitInput",
	  "TransformActionSchemaInput": "org.deltafi.common.types.TransformActionSchemaInput",
	  "TransformInput": "org.deltafi.common.types.TransformInput",
	  "UniqueKeyValues": "org.deltafi.core.domain.types.UniqueKeyValues",
	  "ValidateActionSchemaInput": "org.deltafi.common.types.ValidateActionSchemaInput",
	  "Variable": "org.deltafi.common.types.Variable",
	  "VariableInput": "org.deltafi.common.types.Variable"
	]
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

dependencies {
	implementation(platform("com.netflix.graphql.dgs:graphql-dgs-platform-dependencies:${dgsVersion}")) {
		because 'DGS BOM to automatically define versions of other packages below'
	}

	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	implementation project(':deltafi-common')

	implementation "javax.inject:javax.inject:1"

	implementation "org.springframework.boot:spring-boot-starter-data-mongodb"
	implementation "org.springframework.boot:spring-boot-starter-aop"
	implementation "org.springframework.retry:spring-retry"
	implementation "org.springframework.cloud:spring-cloud-config-server"

	implementation "redis.clients:jedis:${jedisVersion}"

	implementation "io.minio:minio:${minioVersion}"
	implementation 'com.squareup.okhttp3:okhttp:4.10.0'

	implementation "com.networknt:json-schema-validator:1.0.72"
	implementation "com.netflix.graphql.dgs:graphql-dgs-extended-scalars"
	implementation "com.netflix.graphql.dgs:graphql-dgs-spring-boot-starter:${dgsVersion}"
	implementation "org.springframework.boot:spring-boot-starter-web"

	implementation "net.logstash.logback:logstash-logback-encoder:7.2"
	implementation "ch.qos.logback:logback-classic"

	testImplementation "org.springframework.boot:spring-boot-starter-test"

	testImplementation project(':deltafi-common-test')
	testImplementation 'org.awaitility:awaitility:4.2.0'
	testImplementation 'org.hamcrest:hamcrest:2.2'
	testImplementation "org.testcontainers:mongodb:${testContainersVersion}"
	testImplementation "org.testcontainers:testcontainers:${testContainersVersion}"
	testImplementation "org.testcontainers:junit-jupiter:${testContainersVersion}"
}

task copyProbe(type: Copy) {
    from ('.')
    include "probe.sh"
    destinationDir file('build/docker')
}

dockerPrepare {
    dependsOn compileJava
    dependsOn assemble
    finalizedBy copyProbe
}

docker {
    name "${project.name}:${project.version}"
    tag "local", "${localDockerRegistry}/${project.name}:latest"
    copySpec.from("build/libs/").into("build/libs/")
}
