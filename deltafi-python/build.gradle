import com.hierynomus.gradle.license.tasks.LicenseCheck
import com.hierynomus.gradle.license.tasks.LicenseFormat

plugins {
    id 'idea'
    id 'license-conventions'
}

task setupVirtualEnvironment(type: Exec) {
    workingDir 'src'
    commandLine '../bin/native_python', '-m', 'venv', 'venv'
}

task setupPoetry(type: Exec) {
    dependsOn setupVirtualEnvironment
    workingDir 'src'
    commandLine 'venv/bin/pip', '-q', 'install', 'poetry'
}

task copyTemplate(type: Copy) {
    from file('src/pyproject.toml.template')
    into 'src'
    rename { 'pyproject.toml' }
}

task poetryVersion(type: Exec) {
    dependsOn copyTemplate, setupPoetry
    workingDir 'src'
    String pythonVersion = version.replaceAll('-.*', "rc${new Date().getTime()}")
    commandLine 'venv/bin/poetry', 'version', "${pythonVersion}"
}

task cleanDist(type: Delete) {
    delete 'src/dist'
    delete 'src/poetry.lock'
}

task cleanVirtualEnvironment(type: Delete) {
    delete 'src/venv'
}

task poetryInstall(type: Exec) {
    dependsOn poetryVersion
    workingDir 'src'
    commandLine 'venv/bin/poetry', 'install', '--with', 'test'
}

task poetryBuild(type: Exec) {
    dependsOn cleanDist, poetryInstall
    workingDir 'src'
    commandLine 'venv/bin/poetry', 'build'
}

task testPython(type: Exec) {
    dependsOn poetryBuild
    workingDir 'src'
    commandLine 'venv/bin/poetry', 'run', 'pytest', '--junit-xml=../build/test-results/test/TEST-results.xml'
}

task publishToMavenLocal(type: Exec) {
    dependsOn poetryBuild
    workingDir 'src'
    commandLine 'venv/bin/pip', 'install', '.'
}

clean.dependsOn cleanDist
clean.dependsOn cleanVirtualEnvironment

assemble.dependsOn poetryBuild

test {
    it.dependsOn testPython
}

task licenseLocal(type: LicenseCheck) {
    source = fileTree(dir: ".").include("**/*.py")
}
licenseLocal.dependsOn copyTemplate

task licenseFormatLocal(type: LicenseFormat) {
    source = fileTree(dir: ".").include("**/*.py")
}
licenseFormat.dependsOn licenseFormatLocal
