plugins {
    id 'com.palantir.docker'
}

docker {
    name "${project.name}:${project.version}"
    tag "deltafi", "deltafi/${project.name}:${project.version}"
}

if (!deltafiConfig.isEmpty()) {

    var orchestrationMode = deltafiConfig['orchestrationMode'] ? deltafiConfig['orchestrationMode'].toLowerCase() : null

    task installDockerImages {
        group = 'install'
        if (orchestrationMode == 'compose') {
            dependsOn 'dockerTagDeltafi'
        } else if (orchestrationMode == 'kind') {
            dependsOn 'pushToKind'
            mustRunAfter rootProject.installPrerequisites
        } else {
            println ('WARNING: Cannot install docker images for orchestration mode:' + deltafiConfig['orchestrationMode'])
        }
    }

    if (orchestrationMode == 'compose') {
        installDockerImages.finalizedBy rootProject.syncComposeServices
    }

    if (orchestrationMode == 'kind') {
        tasks.findByName('dockerTagDeltafi')?.dependsOn(rootProject.installPrerequisites)
        installDockerImages.finalizedBy rootProject.syncKindPods
    }

    install.dependsOn 'installDockerImages'

    if (orchestrationMode == 'kind') {
        task pushToKind(type: Exec) {

            group = 'install'
            description = 'Load the docker image into KinD'
            commandLine "deltafi", "kind", "load", "deltafi/${project.name}:${project.version}"
            dependsOn 'dockerTagDeltafi'
        }
    }

}

