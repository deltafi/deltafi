plugins {
	id 'com.netflix.dgs.codegen' version '5.0.5'
	id 'com.github.ben-manes.versions' version '0.38.0'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'jacoco'
	id 'maven-publish'
	id 'org.springframework.boot' version '2.4.5'
    id "com.palantir.docker" version "0.22.1"
}

java {
    sourceCompatibility = javaSourceCompatibility
    targetCompatibility = javaTargetCompatibility
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

task renameJar (type: Copy) {
	from ('build/libs/')
	include "deltafi-dgs-${project.version}.jar"
	destinationDir file('build/libs/')
	rename "deltafi-dgs-${project.version}.jar", 'deltafi-dgs.jar'
}

assemble.finalizedBy(renameJar)

jar {
	enabled = true
	archiveClassifier.set('api')
	include('org/deltafi/dgs/api/**')
	include('org/deltafi/dgs/generated/client/**')
	include('org/deltafi/dgs/generated/types/**')
}

configurations {
	jars
}

artifacts {
	jars jar
}

generateJava {
    packageName = 'org.deltafi.dgs.generated'
    generateClient = true
    typeMapping = [
      deltaFile: "org.deltafi.dgs.api.types.DeltaFile",
	  "JSON": "org.deltafi.dgs.api.types.JsonMap"
    ]
}

publishing {
	publications {
		java(MavenPublication) {
			artifact jar
		}
	}
	repositories {
		if(project.hasProperty('gitLabTokenType') && project.hasProperty('gitLabToken')) {
			maven {
				url projectMavenRepo
				credentials(HttpHeaderCredentials) {
					// the following variable reside in ~/.gradle/gradle.properties
					name = gitLabTokenType
					value = gitLabToken
				}
				authentication {
					header(HttpHeaderAuthentication)
				}
			}
		}
	}
}

dependencies {
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	implementation project(":deltafi-common")
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.springframework.retry:spring-retry'
	implementation 'io.fabric8:kubernetes-client:5.4.1'

	implementation 'redis.clients:jedis:3.6.1'

	// Use DGS BOM to automatically define versions of other packages below
	implementation(platform('com.netflix.graphql.dgs:graphql-dgs-platform-dependencies:4.5.0')) {
		because 'DGS BOM to automatically define versions of other packages below'
	}

	implementation 'com.netflix.graphql.dgs:graphql-dgs-extended-scalars'
	implementation 'com.netflix.graphql.dgs:graphql-dgs-spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	implementation 'net.logstash.logback:logstash-logback-encoder:6.6'
	implementation 'ch.qos.logback:logback-classic'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.2.0'
	testImplementation 'io.fabric8:kubernetes-server-mock:5.4.1'
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.enabled true
		csv.enabled true
		html.destination layout.buildDirectory.dir('jacocoHtmlReport').get().asFile
	}

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: ['org/deltafi/dgs/generated/**', '**/*Application.class'])
		}))
	}

}

jacoco {
	reportsDirectory.set(layout.buildDirectory.dir('jacocoReport'))
}

docker {
    dependsOn compileJava
    dependsOn build
    name "${project.name}:${project.version}"
    copySpec.from("build/libs/").into("build/libs/")
}
