plugins {
    id "com.github.hierynomus.license" version "${hierynomusLicenseVersion}"
    id 'java-library'
    id 'maven-publish'
}

description = 'Shared utility library for DeltaFi core'

license {
    header(rootProject.file('HEADER'))
	  excludes(excludeFromLicense)
}

javadoc {
    options.tags = ["ParametersAreNonnullByDefault"]
}

compileJava.dependsOn(processResources)

dependencies {
    api "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
    api "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    api(platform("com.netflix.graphql.dgs:graphql-dgs-platform-dependencies:${dgsVersion}"))
    api 'com.netflix.graphql.dgs:graphql-dgs-client'
    api "com.netflix.graphql.dgs.codegen:graphql-dgs-codegen-client-core:${dgsCodegenClientVersion}"
    api "commons-io:commons-io:2.11.0"
    api(platform("io.github.openfeign:feign-bom:12.1"))
    api "io.github.openfeign:feign-core"
    api "io.github.openfeign:feign-jackson"
    api "io.github.openfeign:feign-slf4j"
    api "io.minio:minio:${minioVersion}"
    api 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    api 'org.apache.commons:commons-lang3:3.12.0'
    api 'org.json:json:20230227'
    api(platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"))
    api "org.springframework.boot:spring-boot-starter-aop"
    api "org.springframework.boot:spring-boot-starter-logging"
    api 'org.springframework.boot:spring-boot-autoconfigure'
    api 'org.springframework.data:spring-data-mongodb'
    api "org.springframework.retry:spring-retry"
    api "redis.clients:jedis"
    api 'org.apache.nifi:nifi-flowfile-packager:1.18.0'

    implementation 'org.apache.commons:commons-text'
    constraints {
        implementation('org.apache.commons:commons-text:1.10.0') {
            because 'CVE-2022-42889 https://security.snyk.io/vuln/SNYK-JAVA-ORGAPACHECOMMONS-3043138'
        }
    }
    implementation 'org.yaml:snakeyaml'
    constraints {
        implementation('org.yaml:snakeyaml:1.33') {
            because 'CVE-2022-25857 https://security.snyk.io/vuln/SNYK-JAVA-ORGYAML-2806360'
        }
    }

    compileOnly "org.springframework.boot:spring-boot-starter-cache"
    testImplementation "org.springframework.boot:spring-boot-starter-cache"
    compileOnly 'com.github.ben-manes.caffeine:caffeine:3.1.4'
    testImplementation 'com.github.ben-manes.caffeine:caffeine:3.1.4'

    implementation 'ch.qos.logback:logback-classic:1.2.11'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'net.logstash.logback:logstash-logback-encoder:7.2'
    implementation 'org.apache.maven:maven-artifact:3.8.7'

    testImplementation project(':deltafi-common-test')
    testImplementation 'org.assertj:assertj-core:3.23.1'
    testImplementation "org.junit.jupiter:junit-jupiter-api:${jupiterVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${jupiterVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${jupiterVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoJupiterVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "com.github.tomakehurst:wiremock-jre8-standalone:2.35.0"
}
