plugins {
    id "com.github.hierynomus.license" version "${hierynomusLicenseVersion}"
    id 'org.springframework.boot' version "${springBootVersion}"
    id "com.palantir.docker" version "${palantirDockerVersion}"
}

license {
    header(rootProject.file('HEADER'))
    excludes(excludeFromLicense)
    strictCheck true
    mapping('java', 'SLASHSTAR_STYLE')
}

dependencies {
    implementation project(':deltafi-action-kit')

    implementation 'org.apache.commons:commons-compress:1.24.0'
    implementation 'org.tukaani:xz:1.9'
    implementation 'org.apache.nifi:nifi-flowfile-packager:1.23.2'
    implementation 'org.apache.tika:tika-core:2.9.0'
    implementation 'com.bazaarvoice.jolt:jolt-complete:0.1.8'
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${jacksonVersion}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:${jacksonVersion}"
    implementation 'com.jayway.jsonpath:json-path:2.8.0'

    testImplementation project(':deltafi-common-test')
    testImplementation project(':deltafi-action-kit-test')

    testImplementation 'io.rest-assured:rest-assured:5.3.2'
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoJupiterVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${jupiterVersion}"
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${jupiterVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${jupiterVersion}"
    testImplementation 'org.assertj:assertj-core:3.24.2'
}

jar {
    // do not create the -plain.jar
    enabled = false
}

springBoot {
    buildInfo {
        properties {
            additional = [
                    description: "Core DeltaFi actions",
                    actionKitVersion: "${project.version}"
           ]
        }
    }
}

dockerPrepare {
    dependsOn assemble
}

docker {
    name "${project.name}:${project.version}"
    tag "local", "${localDockerRegistry}/${project.name}:latest"
    copySpec.from(bootJar.archiveFile).into("build/libs/")
}

test.dependsOn(":deltafi-action-kit-test:renameJar")
