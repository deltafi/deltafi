plugins {
    id "com.github.hierynomus.license" version "${hierynomusLicenseVersion}"
    id 'org.springframework.boot' version "${springBootVersion}"
    id "com.palantir.docker" version "${palantirDockerVersion}"
}

license {
    header(rootProject.file('HEADER'))
    excludes(excludeFromLicense)
    strictCheck true
    mapping('java', 'SLASHSTAR_STYLE')
}

dependencies {
    implementation project(':deltafi-action-kit')

    implementation 'org.apache.commons:commons-compress:1.26.1'
    implementation 'org.tukaani:xz:1.9'
    implementation 'org.apache.tika:tika-core:2.9.1'
    implementation 'com.bazaarvoice.jolt:jolt-complete:0.1.8'
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${jacksonVersion}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:${jacksonVersion}"
    implementation 'com.github.mwiede:jsch:0.2.17'
    implementation 'com.jayway.jsonpath:json-path:2.9.0'
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.77'

    testImplementation project(':deltafi-common-test')
    testImplementation project(':deltafi-action-kit-test')

    testImplementation 'io.rest-assured:rest-assured:5.4.0'
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoJupiterVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${jupiterVersion}"
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${jupiterVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${jupiterVersion}"
    testImplementation 'org.assertj:assertj-core:3.25.3'
}

jar {
    // do not create the -plain.jar
    enabled = false
}

springBoot {
    buildInfo {
        properties {
            additional = [
                    description: "Core DeltaFi actions",
                    actionKitVersion: "${project.version}"
           ]
        }
    }
}

dockerPrepare {
    dependsOn assemble
}

docker {
    name "${project.name}:${project.version}"
    tag "local", "${localDockerRegistry}/${project.name}:latest"
    tag "deltafi", "deltafi/${project.name}:${project.version}"
    copySpec.from(bootJar.archiveFile).into("build/libs/")
}
